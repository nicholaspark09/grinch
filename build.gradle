buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath 'digital.wup:android-maven-publish:3.6.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: 'versions.gradle'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'
group = 'com.github.nicholaspark09'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode versions.versionCode
        versionName versions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    api(
            // AndroidX Test
            deps.xTest.rules,
            deps.xTest.runner,
            deps.xTest.orchestrator,
            deps.xTest.jUnit,

            // Espresso
            deps.espresso.core,
            deps.espresso.contrib,
            deps.espresso.idling,

            // Utils
            deps.utils.uiautomator,
            deps.utils.hamcrest
    )
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        android.libraryVariants.all { variant ->
            "maven${variant.name.capitalize()}Aar"(MavenPublication) {
                from components.android
                groupId 'com.cincinnatiai.grinch'
                artifactId "grinchwalker"
                version "${versions.versionName}"
            }
        }
    }
}